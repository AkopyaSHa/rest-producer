plugins {
    id 'org.springframework.boot' version '2.3.1.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'java'
    id "org.openapi.generator" version "4.3.1"
    id "org.asciidoctor.jvm.convert" version "3.2.0"
}

group 'ru.uip'
version '1.0-SNAPSHOT'
sourceCompatibility = '11'

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-reactor-netty'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    implementation "ru.uip:model:1.0-SNAPSHOT"

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

openApiGenerate {
    generatorName = "asciidoc"
    inputSpec = "$projectDir/src/main/api/api.yaml".toString()
    templateDir = "$projectDir/src/main/api/template".toString()
    outputDir = "$buildDir/generated-docs".toString()
    configOptions = [
            specDir: "$projectDir/src/main/api/template".toString(),
            snippetDir: "$projectDir/src/main/api/template".toString()
    ]
}

asciidoctor {
    sourceDir  file("${buildDir}/generated-docs")
    outputDir  file('build/docs')
}

build.dependsOn("asciidoctor")
asciidoctor.dependsOn("openApiGenerate")

